#INCLUDE "FINR280.CH"
#INCLUDE "PROTHEUS.CH"

// 17/08/2009 - Compilacao para o campo filial de 4 posicoes
// 18/08/2009 - Compilacao para o campo filial de 4 posicoes

/*


Ŀ
 DATA    BOPS Prograd.ALTERACAO                                      
Ĵ
27.06.98xxxxxxMauricioNumero de titulos com 12 posicoes              
02.10.98xxxxxxAndreia  Acerto no lay-out p/ ativar set century  		
30.03.99META  Julio    Verificacao nos Parametros de Tamanho  do Rel.
30.11.99META  Julio    Melhoria de Performance - Implementar TcQuery 
30.11.99META  Julio    Considerar Filtro de Usuario                  
Ĵ
30.11.99META  Julio    Reviso dos Fontes / Remover cdigo DOS       
21.08.00ooooooRubens PImplementacao multimoeda                       
29.06.06R4		Daniel   Construcao do Release 4		                  
ٱ


*/
/*/


Ŀ
Funo     FINR280   Autor  Daniel Tadashi Batori  Data  29.06.06 
Ĵ
Descrio  Relacao dos Juros Recebidos                                
Ĵ
Sintaxe e  FINR280(void)                                              
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ


/*/
USER  Function TFINR280()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	oReport := ReportDef()
	oReport:PrintDialog()
Endif

Return

/*


Ŀ
Funo     ReportDef Autor  Daniel Batori          Data  29/06/06 
Ĵ
Descrio  Definicao do layout do Relatorio									  
Ĵ
Sintaxe    ReportDef(void)                                            
Ĵ
 Uso       Generico                                                   
ٱ


*/
Static Function ReportDef()
Local cPerg      := "FIN280"  // Nome do grupo de perguntas
Local oReport  
Local oSection1
Local cAliasQry1 := "SE5" //GetNextAlias()
Local aTam1, aTam2, aTam3
Local nMoedaBco := 1

Pergunte(cPerg, .F.)

oReport := TReport():New("FINR280",STR0005,cPerg,;
{|oReport| ReportPrint(oReport,@cAliasQry1)},STR0001+' '+STR0002)


aTam1 := TamSX3("E1_CLIENTE")
aTam2 := TamSX3("E1_LOJA")
nTamCli :=	aTam1[1] + aTam2[1] + 1
aTam1 := TamSX3("E5_PREFIXO")
aTam2 := TamSX3("E5_NUMERO")
aTam3 := TamSX3("E5_PARCELA")
nTamTit :=	aTam1[1] + aTam2[1] + aTam3[1] + 2

oSection1 := TRSection():New(oReport,STR0010,{"SE5","SE1","SA1","SED","SA6"},)
TRCell():New(oSection1,"E5_BANCO" 	,"SE5",STR0011,,,.F.,{|| (cAliasQry1)->E5_BANCO})		//"BANCO"
TRCell():New(oSection1,"CLIENLOJA" 	,     ,STR0012,,nTamCli,.F.,/*{|| 	SE1->E1_CLIENTE+' '+SE1->E1_LOJA }*/)	//"CODIGO"
TRCell():New(oSection1,"A1_NREDUZ" 	,"SA1",STR0013,,,.F.,{|| SA1->A1_NREDUZ})	//"CLIENTE"
TRCell():New(oSection1,"E1_NUM"   	,"SE5",STR0014,,nTamTit,.F.,{|| (cAliasQry1)->E5_PREFIXO+"-"+(cAliasQry1)->E5_NUMERO+"-"+(cAliasQry1)->E5_PARCELA})		//"No.TITULO"
TRCell():New(oSection1,"E1_VENCTO"	,"SE1",STR0015,,,.F.,{|| SE1->E1_VENCTO })		//"VENCTO"
TRCell():New(oSection1,"ATRASO"   	,		,STR0016,,5,.F.,{||})	//"ATRASO"
TRCell():New(oSection1,"E5_DATA"  	,"SE5",STR0017,,,.F.,{|| (cAliasQry1)->E5_DATA })		//"PAGTO"
TRCell():New(oSection1,"E1_VLCRUZ"	, "SE1",STR0018,TM(SE1->E1_VLCRUZ,14,MsDecimais(1)),,.F.,/*{||}*/)
TRCell():New(oSection1,"E1_VALJUR"	,"SE1",STR0019,TM(SE1->E1_VLCRUZ,14,MsDecimais(1)),,.F.,{|| nMoedaBco := If(cPaisLoc#"BRA",Max(SA6->A6_MOEDA,1),1),;		//"VALOR JUROS"
																											 xMoeda((cAliasQry1)->E5_VALOR, nMoedaBco, mv_par05, (cAliasQry1)->E5_DATA, MsDecimais(mv_par05)+1) })		

#IFNDEF TOP
	TRPosition():New ( oSection1, "SE1" , 1 ,{|| xfilial("SE1")+SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO) } , .T. ) 
	TRPosition():New ( oSection1, "SA1" , 1 ,{|| xfilial("SA1")+SE1->(E1_CLIENTE+E1_LOJA) } , .T. ) 
	TRPosition():New ( oSection1, "SA6" , 1 ,{|| xfilial("SA6")+SA6->(E5_BANCO+E5_AGENCIA+E5_CONTA) } , .T. ) 
#ELSE
	oSection1:SetLineCondition({|| mv_par06 == 1 .OR. mv_par05==Max(SA6->A6_MOEDA,1) })
#ENDIF

                                                      
oReport:SetTotalText(STR0008)
TRFunction():New(oSection1:Cell("E1_VALJUR"),"TOTAL","SUM",,,,,.F.,.T.)
oReport:SetTotalInLine(.F.)
oSection1:SetTotalInLine(.F.)

Return oReport                                                                              

/*


Ŀ
Programa  ReportPrint Autor Daniel Batori           Data 26/06/2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os  
          relatorios que poderao ser agendados pelo usuario.           
Ĵ
Retorno   Nenhum                                                       
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                            
Ĵ
   DATA    Programador   Manutencao efetuada                          
Ĵ
                                                                      
ٱ


*/
Static Function ReportPrint(oReport,cAliasQry1)
Local oSection1  := oReport:Section(1)
Local cCond1	:=	""
Local cCond2	:=	""
Local nMoedaBCO:= 1
Local aAreaAtu := {}
//Local aAreaSE1 := {}
//Local lOk := .F.
Local cFilterUser := ""

dbSelectArea("SE5")
dbSetOrder(1)
  
oReport:SetMeter(RecCount())
dbSeek( cFilial + DtoS(mv_par01),.T.)

cCond1:= '!Eof() .And. E5_FILIAL == "'+cFilial+'" .And. '+;
'Dtos(E5_DATA) <= "'+Dtos(mv_par02)+'"'
	
cCond2:= 'E5_RECPAG != "P" .and. E5_SITUACA != "C" .And. '+;
'(E5_TIPODOC == "JR" .or. E5_TIPODOC == "J2") .And. '+;
'Dtos(E5_DTDIGIT) >= "'+dtos(mv_par03)+'" .and. '+;
'Dtos(E5_DTDIGIT) <= "'+dtos(mv_par04)+'"'
	
If !Empty(oSection1:GetADVPLExp("SE5"))
	cCond2 += " .And. " + oSection1:GetADVPLExp("SE5") + " .And. Dtos(E5_DATA) >= " + '"' + Dtos(mv_par01) + '" .And. Dtos(E5_DATA) <= "' + Dtos(mv_par02) + '"' 
Else
	cFilterUser := oSection1:GetADVPLExp("SE1")	
EndIf

oSection1:Init()
While &cCond1
	//Pega moeda do banco e calcula o valor na moeda
	oReport:IncMeter()
	If oReport:Cancel()
		Exit
	EndIf
		
	nMoedaBCO	:=	1
		
	If cPaisLoc	# "BRA"
		SA6->(DbSetOrder(1))
		SA6->(DbSeek(xFilial()+SE5->E5_BANCO+SE5->E5_AGENCIA+SE5->E5_CONTA))
		nMoedaBco := Max(SA6->A6_MOEDA,1)
	Endif

    // Desconsidera movimentos de outras moedas
	If mv_par06 == 2 .And. nMoedaBco != mv_par05 
		(cAliasQry1)->(dbSkip())
		Loop
	EndIf                      
		
	IF &cCond2		
		aAreaAtu := GetArea()			
		dbSelectArea("SE1")
		SE1->(dbgotop())	
		
		SE1->(DbSetOrder(2))
		SE1->(DbSeek( xFilial("SE1") + (cAliasQry1)->E5_CLIFOR + (cAliasQry1)->E5_LOJA + (cAliasQry1)->E5_PREFIXO + (cAliasQry1)->E5_NUMERO + (cAliasQry1)->E5_PARCELA ))
		
		If !Empty(cFilterUser)										
			While !SE1->(EOF())  
				If (&(cFilterUser)) .And. (SE1->E1_NUM == (cAliasQry1)->E5_NUMERO .And. SE1->E1_PREFIXO == (cAliasQry1)->E5_PREFIXO .And. SE1->E1_LOJA == (cAliasQry1)->E5_LOJA) 						
					TFr280Vlr(oSection1,(cAliasQry1)->E5_CLIFOR,(cAliasQry1)->E5_LOJA,(cAliasQry1)->E5_PREFIXO,(cAliasQry1)->E5_NUMERO,(cAliasQry1)->E5_PARCELA,(cAliasQry1)->E5_TIPO,(cAliasQry1)->E5_DATA)
					oSection1:PrintLine()	
					exit					
				EndIf	
																
				SE1->(dbSkip())	
			EndDo																
		Else										
			While !SE1->(EOF())  
				If SE1->E1_NUM == (cAliasQry1)->E5_NUMERO .And. SE1->E1_PREFIXO == (cAliasQry1)->E5_PREFIXO .And. SE1->E1_LOJA == (cAliasQry1)->E5_LOJA						
					TFr280Vlr(oSection1,(cAliasQry1)->E5_CLIFOR,(cAliasQry1)->E5_LOJA,(cAliasQry1)->E5_PREFIXO,(cAliasQry1)->E5_NUMERO,(cAliasQry1)->E5_PARCELA,(cAliasQry1)->E5_TIPO,(cAliasQry1)->E5_DATA)
					oSection1:PrintLine()
					exit
				EndIf
			
				SE1->(dbSkip())				
			EndDo
		EndIf	
			
		RestArea(aAreaAtu)
		aAreaAtu := {}				
	Endif		
	
	(cAliasQry1)->(DbSkip())
Enddo
	
oSection1:Finish()
Return

Static Function TFr280Vlr(oSection1,cCli,cLoja,cPref,cNum,cParc,cTipo,dDataBx)
Local aAreaSA1 := SA1->(getArea()) 
Local oSection := oSection1

oSection:Cell("E1_VLCRUZ"):SetValue(xMoeda(SE1->E1_VLCRUZ, SE1->E1_MOEDA, mv_par05, dDataBx, MsDecimais(mv_par05)+1))		
oSection:Cell("CLIENLOJA"):SetValue(SE1->E1_CLIENTE+' '+SE1->E1_LOJA)
oSection:Cell("E1_VENCTO"):SetValue(SE1->E1_VENCTO)
oSection:Cell("ATRASO"):SetValue(dDataBx - SE1->E1_VENCTO)	//"ATRASO"
		
DbSelectArea("SA1")
DbSetOrder(1)                  
	
If Dbseek(xFilial("SA1") + cCli+cLoja)
	oSection:Cell("A1_NREDUZ"):SetValue(SA1->A1_NREDUZ)
EndIf
	
SA1->(RestArea(aAreaSA1))
Return




